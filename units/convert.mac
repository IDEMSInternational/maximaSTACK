/* This library depends on vectors.mac */

/*
  We use matrix([null]) as an indicator for invalid input.
  This has the issue that
  lin_ind_colsp(matrix([null])) and
  same_colspacep(matrix([null]), matrix([1]))
  would naturally evaluate to true, even though they shouldn't.
  This is fixed via an explicit check.
  Ideally, we would return matrix() instead but alas, STACK doesn't like that:
  (%i4)
  (%i5)
  	#0: stack_matrix_disp(m=matrix())
  	#1: StackDISP(expr=matrix(),exprm=)
  	#2: _CS2l(_k=qq,_v=matrix())
  (%i6) TIMEDOUT
*/

/**
 * Try to convert object to a column vector.
 * Supported objects include:
 * * Mx1 matrices
 * * Vectors using c notation
 * * Flat lists, ntuples (with entries interpreted as a column vector entries)
 * * TODO: Check that list is, indeed, flat, and matrix does not contain list as entries
 * If conversion is not possible, return matrix([null])
 *
 * @param[ex] ex 
 * @return[matrix] A Mx1 matrix representing the column vector, or matrix([null]) if input was invalid
 */
convert_to_colvec(ex):= block([op1],
  ex: vec_convert(ex),
  if col_vecp(ex) then return(ex),
  if vectorp(ex) then return(matrix([null])),
  op1: safe_op(ex),
  if member(op1,["[","ntuple"]) then return(transpose(apply(matrix,[ex]))),
  return(matrix([null]))
);

 /**
 * Takes a collection of vectors and return the matrix that has those vectors as columns.
 * If the input is already a matrix, the matrix itself is returned.
 * If the input in invalid, matrix([null]) is returned.
 * The rationale is that further operations can still be performed on the output without
 * crashing in the feedback variables, and we can catch this case in the first PRT node.
 * The vectors themselves may be different objects. Supported objects include:
 * * Mx1 matrices
 * * Vectors using c notation
 * * Lists, ntuples (representing a column vector)
 * The collection may be a list, set, ntuple, span, or matrix
 * If given a matrix, then returns the columns as a list, with each column also being a list.
 *
 * @param[] ex A list, set, ntuple or span containing vectors as matrices, lists, c, r, sets or ntuples, or a matrix
 * @return[matrix] A matrix containing the input elements as columns, or matrix([null]) in case of invalid input.
 */
cols_to_matrix(L):=block([op1, colmat],
  if matrixp(L) then return(L),
  op1: safe_op(L),
  if not(member(op1,["[","ntuple","{","span"])) then return(matrix([null])),
  L: map(lambda([elem],convert_to_colvec(elem)),L),
/*  if is(cardinality(setify(map(length,L))) # 1) then return(matrix([null])),*/
  colmat: errcatch(ev(apply(addcol, L), simp)),
  if emptyp(colmat) then return(matrix([null])) else first(colmat)
);

 /**
 * Takes a matrix and returns a list of its column vectors.
 *
 * @param[matrix] M 
 * @return[list] List of Mx1 matrices (column vectors)
 */
matrix_to_cols(M):= makelist(col(M, i), i, 1, second(matrix_size(M)));

 /**
 * Convert input to a list of column vectors of the same dimension.
 * Returns [] if the input is invalid.
 *
 * @param[matrix] M A matrix or list of vectors (see cols_to_matrix)
 * @return[list] List of Mx1 matrices (column vectors)
 */
to_list_of_column_vectors(L):=block([M],
    M: cols_to_matrix(L),
    if M = matrix([null]) then return([]),
    return(matrix_to_cols(M))
);

 /**
 * Are the columns of this matrix linearly independent?
 *
 * @param[] M A matrix or list of vectors (see cols_to_matrix)
 * @return[boolean] 
 */
lin_ind_colsp(M):=block([],
    M: cols_to_matrix(M),
    if M = matrix([null]) then return(false),
    return(is(rank(M)#0 and rank(M)=second(matrix_size(M))))
);

 /**
 * Is the column space of M1 and M2 the same?
 *
 * @param[] M1 A matrix or list of vectors (see cols_to_matrix)
 * @param[] M2 A matrix or list of vectors (see cols_to_matrix)
 * @return[boolean] 
 */
same_colspacep(M1,M2):=block([combined, rank_combined],
    M1: cols_to_matrix(M1),
    M2: cols_to_matrix(M2),
    if M1 = matrix([null]) or M2 = matrix([null]) then return(false),
    if length(M1) # length(M2) then return(false),
    combined: errcatch(addcol(M1,M2)),
    if emptyp(combined) then return(false),
    rank_combined: rank(first(combined)),
    is(rank(M1)=rank_combined and rank(M2)=rank_combined)
);
